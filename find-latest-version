#!/usr/local/cpanel/3rdparty/bin/perl
# cpanel - find-latest-version                  Copyright(c) 2022 cPanel, L.L.C.
#                                                           All rights Reserved.
# copyright@cpanel.net                                         http://cpanel.net
# This code is subject to the cPanel license. Unauthorized copying is prohibited

package ea_nginx_njs::find_latest_version;

use strict;
use warnings;

use lib "../ea-tools/lib/ea4_tool";    # assumes ea-tools is checked out next to this repo
use ea4_tool::util ();

ea4_tool::util::find_latest_version( \&_get_required, \&_add_sum ) if !caller();

###############
#### helpers ##
###############

sub _get_required {
    my ($http) = @_;

    my $res = $http->get("https://api.github.com/repos/nginx/njs/tags");
    if ( !$res->{success} ) {
        die "Could not GET ngx_njs info from github ($res->{status} $res->{reason})\n\t Throttled? `curl -I https://api.github.com/rate_limit 2>&1 | grep ^X-RateLimit`\n";
    }

    # get $version, $url, $name from $res
    my $version;
    for my $tag ( @{ Cpanel::JSON::Load( $res->{content} ) } ) {
        next if $tag->{name} !~ m/^\d+\.\d+\.\d+$/;
        $version = $tag->{name};
        last;
    }

    die "Could not determine version from github\n" if !$version;

    my $name = "$version.tar.gz";                                                  # needs to match $version
    my $url  = "https://github.com/nginx/njs/archive/refs/tags/$version.tar.gz";

    return ( $version, $url, $name );
}

sub _add_sum {
    my ( $http, $hr ) = @_;

    # github does not currently have a way to get the sum

    return;
}

__END__

=encoding utf-8

=head1 README

Typical github release-tag pattern
